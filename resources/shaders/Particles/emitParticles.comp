#version 450

struct Particle 
{
	vec3    position;
    float   lifeTime;
    vec4    color;
	vec3    velocity;
    float   angle;
    vec4    auxiliarData;
    vec2    currentOffset;
    vec2    nextOffset;
};

layout(std140, binding = 0) buffer InputSSBO 
{
   Particle particlesIn[ ];
};

layout(std140, binding = 1) buffer OutputSSBO 
{
   Particle particlesOut[ ];
};

layout(std430, binding = 2) buffer DeadParticlesSSBO 
{
    int deadParticles[];
};

layout(set = 0, binding = 3) uniform UniformParticleSystem
{
    vec4  initialColor;
    float particleLifeTime;
    float particleSystemDuration;
    float particlePerFrame;
    float gravity;
    float emissionAngle;
    float emissionRadius;
    float speed;
    float angularSpeed;
    float initAngle;
    float initSize;
    float auxData;
    uint  maxParticles;
} uboParticleSystem;

layout(set = 0, binding = 4) uniform UniformNewParticles
{
    uint  newParticles;
    uint  frameCount;
} uboNewParticles;

uint wang_hash(uint seed)
{
    uint resultSeed = seed;
    resultSeed = (resultSeed ^ 61) ^ (resultSeed >> 16);
    resultSeed *= 9;
    resultSeed = resultSeed ^ (resultSeed >> 4);
    resultSeed *= 0x27d4eb2d;
    resultSeed = resultSeed ^ (resultSeed >> 15);
    return resultSeed;
}

uint pcg_hash(uint inputValue)
{
    uint state = inputValue * 747796405u + 2891336453u;
    uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
    return (word >> 22u) ^ word;
}

float random(float seed) 
{
    return fract(sin(seed) * 43758.5453123);
}

float randomFloatX(uint value)
{
    float result = wang_hash(value);
    return result * (1.0 / 4294967296.0);
}

float randomFloatZ(uint value)
{
    float result = pcg_hash(value);
    return result * (1.0 / 4294967296.0);
}

vec3 ComputeVelocity(uint seed)
{
    vec3 emitCone = vec3(0.0, 1.0, 0.0);
    if (uboParticleSystem.emissionAngle > 0.0)
    {
        float dirX = randomFloatX(seed) * uboParticleSystem.emissionAngle - (uboParticleSystem.emissionAngle * 0.5);
        float dirZ = randomFloatZ(seed) * uboParticleSystem.emissionAngle - (uboParticleSystem.emissionAngle * 0.5);
        emitCone = vec3(dirX, 1.0, dirZ);
    }
    
    return emitCone * uboParticleSystem.speed;
}

vec3 ComputePosition(uint seed)
{
    if (uboParticleSystem.emissionRadius > 0.0)
    {
        float xPos = randomFloatX(seed) * uboParticleSystem.emissionRadius - (uboParticleSystem.emissionRadius * 0.5);
        float zPos = randomFloatZ(seed) * uboParticleSystem.emissionRadius - (uboParticleSystem.emissionRadius * 0.5);
        return vec3(xPos, 0.0, zPos);
    }

    return vec3(0.0);
}

layout (local_size_x = 256, local_size_y = 1, local_size_z = 1) in;
void main() 
{
    uint index = gl_GlobalInvocationID.x;

    if (index < uboNewParticles.newParticles)
    {
        uint seed = (index * 1973 + 9277 + uboNewParticles.frameCount * 26699) | 1;
        
        int idDeadParticle = atomicAdd(deadParticles[deadParticles.length() - 1], -1) - 1;

        if (idDeadParticle < 0)
        {
            atomicAdd(deadParticles[deadParticles.length() - 1], 1);
            return;
        }

        Particle initParticle;
        initParticle.velocity = ComputeVelocity(seed);
        initParticle.position = ComputePosition(seed);
        initParticle.lifeTime = uboParticleSystem.particleLifeTime;
        initParticle.angle = uboParticleSystem.initAngle;
        initParticle.color = uboParticleSystem.initialColor;
        initParticle.auxiliarData.x = uboParticleSystem.initSize;

        int realParticleIndex = deadParticles[idDeadParticle];

        particlesIn[realParticleIndex] = initParticle;
        particlesOut[realParticleIndex] = initParticle;
    }
}